
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.7'
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects  {
    group = 'net.riotopsys.factotum'
    version = '0.0.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: "jacoco"
    apply plugin: 'maven'

    apply plugin: 'pmd'
    apply plugin: 'findbugs'
    apply plugin: 'checkstyle'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    test.finalizedBy(project.tasks.jacocoTestReport)

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    checkstyle{
        configFile = new File(rootDir, "checkstyle.xml")
    }

}

subprojects {

    repositories {
        mavenLocal()
        mavenCentral()
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
